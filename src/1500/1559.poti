mov(dir: Texto, b: Boolean) = se b então dir senão "" fim

pode_mover(a, b: Inteiro) = (a == b) <> (b == 0) 

n = leia_inteiro
para i de 1 até n faça
    linhas = para j de 1 até 4 gere leia_inteiros(" ") fim
    var up, down, left, right := falso
    para x de 1 até 4 faça
        linha = linhas[x]
        coluna = para j de 1 até 4 gere linhas[j][x] fim
        para a de 1 até 3 faca
            left  := left  ou pode_mover(linha[a+1], linha[a])
            right := right ou pode_mover(linha[a], linha[a+1])
            up    := up    ou pode_mover(coluna[a+1], coluna[a])
            down  := down  ou pode_mover(coluna[a], coluna[a+1])
        fim
    fim
   
    jogadas = (mov(" DOWN", down) + mov(" LEFT", left) + mov(" RIGHT", right) + mov(" UP", up)).descarte(1)

    fim_de_jogo = linhas.injete([0])(_ + _).posição(2048) > 0

    se jogadas == "" ou fim_de_jogo então
        escreva "NONE"
    senão
        escreva jogadas
    fim
fim